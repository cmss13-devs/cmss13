// Clickable stat() button.
/obj/statclick
	name = "Initializing..."
	var/target

INITIALIZE_IMMEDIATE(/obj/statclick)

/obj/statclick/Initialize(mapload, text, target) //Don't port this to Initialize it's too critical
	. = ..()
	name = text
	src.target = target

/obj/statclick/proc/update(text)
	name = text
	return src

/obj/statclick/debug
	var/class

/obj/statclick/debug/clicked()
	if(!usr.client.admin_holder || !target)
		return
	if(!class)
		if(istype(target, /datum/controller/subsystem))
			class = "subsystem"
		else if(istype(target, /datum/controller))
			class = "controller"
		else if(istype(target, /datum))
			class = "datum"
		else
			class = "unknown"

	usr.client.debug_variables(target)
	message_admins("Admin [key_name_admin(usr)] is debugging the [target] [class].")
	return TRUE


// Debug verbs.
/client/proc/restart_controller(controller in list("Master", "Failsafe"))
	set category = "Debug.Controllers"
	set name = "Restart Controller"
	set desc = "Restart one of the various periodic loop controllers for the game (be careful!)"

	if(!admin_holder)
		return
	switch(controller)
		if("Master")
			Recreate_MC()
			//SSblackbox.record_feedback("tally", "admin_verb", 1, "Restart Master Controller")
		if("Failsafe")
			new /datum/controller/failsafe()
			//SSblackbox.record_feedback("tally", "admin_verb", 1, "Restart Failsafe Controller")

	message_admins("Admin [key_name_admin(usr)] has restarted the [controller] controller.")

/client/proc/debug_role_authority()
	set category = "Debug.Controllers"
	set name = "Debug Role Authority"

	if(!RoleAuthority)
		to_chat(usr, "RoleAuthority not found!")
		return
	debug_variables(RoleAuthority)
	message_staff("Admin [key_name_admin(usr)] is debugging the Role Authority.")
