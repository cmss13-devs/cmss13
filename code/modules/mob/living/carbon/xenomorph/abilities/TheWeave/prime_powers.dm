/datum/action/xeno_action/activable/weave_bless/use_ability(atom/target)
	var/mob/living/carbon/xenomorph/prime_weaver/self = owner

	if (!action_cooldown_check())
		return FALSE

	if (!self.check_state())
		return FALSE

	var/mob/living/carbon/human/human = target
	if	(!ishuman(human) || !human.allow_gun_usage)
		to_chat(self, SPAN_XENOWARNING("You must target a non believer!"))
		return FALSE

	if (get_dist_sqrd(human, self) > 2)
		to_chat(self, SPAN_XENOWARNING("[target] is too far away!"))
		return FALSE

	if (human.stat == DEAD)
		to_chat(self, SPAN_XENOWARNING("[human] is dead, why would you want to touch them?"))
		return FALSE

	var/datum/hive_status/mutated/weave/nexus = self.hive
	if(!istype(nexus))
		to_chat(self, SPAN_XENOWARNING("You cannot reach The Weave!"))
		return FALSE

	if (!nexus.can_use_energy(plasma_cost))
		to_chat(self, SPAN_XENOWARNING("The Weave is not strong enough here to do that!"))
		return FALSE

	ADD_TRAIT(human, TRAIT_IMMOBILIZED, TRAIT_SOURCE_ABILITY("weave_blessing"))
	human.update_xeno_hostile_hud()

	apply_cooldown()

	ADD_TRAIT(self, TRAIT_IMMOBILIZED, TRAIT_SOURCE_ABILITY("weave_blessing"))
	self.anchored = TRUE

	if (do_after(self, activation_delay, INTERRUPT_ALL | BEHAVIOR_IMMOBILE, BUSY_ICON_HOSTILE))
		if(nexus.use_energy(plasma_cost))
			self.visible_message(SPAN_XENOHIGHDANGER("[self] floods [human]'s mind with The Weave!"), SPAN_XENOHIGHDANGER("You flood the mind of [human] with The Weave!"))

			human.apply_effect(get_xeno_stun_duration(human, 0.5), WEAKEN)

			human.WeaveClaim(CAUSE_WEAVER)
		else
			to_chat(self, SPAN_XENOWARNING("The Weave is not strong enough here to do that!"))

	REMOVE_TRAIT(self, TRAIT_IMMOBILIZED, TRAIT_SOURCE_ABILITY("weave_blessing"))
	self.anchored = FALSE

	unroot_human(human, TRAIT_SOURCE_ABILITY("weave_blessing"))

	. = ..()
	return TRUE


/datum/action/xeno_action/onclick/weave_heal/use_ability(atom/atom)
	var/mob/living/carbon/xenomorph/self = owner
	if (!action_cooldown_check())
		return FALSE

	if (!self.check_state())
		return FALSE

	var/datum/hive_status/mutated/weave/nexus = self.hive
	if(!istype(nexus))
		to_chat(self, SPAN_XENOWARNING("You cannot reach The Weave!"))
		return FALSE

	self.visible_message(SPAN_XENONOTICE("[self] begins to focus their energy!"), SPAN_XENONOTICE("You start to focus your energies!"))
	if (do_after(self, 10 SECONDS, INTERRUPT_ALL | BEHAVIOR_IMMOBILE, BUSY_ICON_MEDICAL))
		if (!check_and_use_plasma_owner())
			to_chat(self, SPAN_XENOWARNING("You do not have enough plasma stored to do this. You have [self.plasma_stored]/[plasma_cost]!"))
			return FALSE

		self.visible_message(SPAN_XENONOTICE("[self] radiates healing energies!"), SPAN_XENONOTICE("You release some of your energy into a powerful healing aura!"))

		var/list/target_list = list()
		for(var/mob/living/carbon/possible_target in view(5, self))
			if(possible_target == self || !possible_target.client || !istype(possible_target))
				continue
			target_list += possible_target
		for(var/mob/living/carbon/target in target_list)
			if(HAS_TRAIT(target, TRAIT_WEAVE_SENSITIVE))
				target.apply_damage(BRUTE, -40)
				target.apply_damage(BURN, -40)
				to_chat(target, SPAN_XENOBOLDNOTICE("You have been regenerated by the The Weave!"))
			else
				to_chat(target, SPAN_XENOBOLDNOTICE("You feel a ripple as energies pass over you!"))
	else
		self.visible_message(SPAN_XENOWARNING("[self] decides not to release their energy."), SPAN_XENOWARNING("You decide not to release your energy."))

	. = ..()
	return TRUE
